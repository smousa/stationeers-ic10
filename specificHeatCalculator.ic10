# Initialize Specific Heat Values
define ArrayLen 6
define ElementLen 3
move sp 0
push LogicType.RatioOxygen
push LogicType.RatioLiquidOxygen
push 21.1 # OxygenSpecificHeat
push LogicType.RatioNitrogen
push LogicType.RatioLiquidNitrogen
push 20.6 # NitrogenSpecificHeat
push LogicType.RatioCarbonDioxide
push LogicType.RatioLiquidCarbonDioxide
push 28.2 # CarbonDioxideSpecificHeat
push LogicType.RatioPollutant
push LogicType.RatioLiquidPollutant
push 24.8 # PollutantSpecificHeat
push LogicType.RatioNitrousOxide
push LogicType.RatioLiquidNitrousOxide
push 37.2 # NitrousOxideSpecificHeat
push LogicType.RatioVolatiles
push LogicType.RatioLiquidVolatiles
push 20.4 # VolatilesSpecificHeat

define WaterSpecificHeat 72

# Inputs
alias PipeAnalyzer d0

define SpecificHeat LogicType.Channel0

main:
  l r0 PipeAnalyzer TotalMoles
  beqz r0 done
  move r0 0

  move r15 0 # Loop Iterator
  move r14 0 # Total Ratio
loop:
  get r12 db r15 # LogicType.RatioGas
  l r13 PipeAnalyzer r12
  add r15 r15 1
  get r12 db r15 # LogicType.RatioLiquid
  l r12 PipeAnalyzer r12
  add r13 r13 r12 # Ratio of type
  add r14 r14 r13 # Ratio of total
  add r15 r15 1
  get r12 db r15  # SpecificHeat
  mul r13 r13 r12 # Partial Specific Heat
  add r0 r0 r13   # Total Specific Heat
  add r15 r15 1
  div r12 r15 ElementLen
  blt r12 ArrayLen loop

  # Add Steam/Water/Polluted Water
  sub r13 1 r14
  mul r13 r13 WaterSpecificHeat
  add r0 r0 r13
  
done:
  s db Setting r0
  s PipeAnalyzer:0 SpecificHeat r0
  yield
  j main
