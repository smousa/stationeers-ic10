define SiloMax 600
define OreStackSize 50
define IngotStackSize 500
define PurgeThreshold 0.8

# Structures
define Silo HASH("StructureSDBSilo")
define Stacker HASH("StructureStacker")
define Memory HASH("StructureMemory")
define Switch HASH("StructureLogicSwitch2")

# Networks
define ResourceOre HASH("ItemOre")
define ResourceIngot HASH("ItemIngot")

# Iterables
alias OreIndex r0
alias IngotIndex r1
move sp 0
push HASH("ItemIce")
push HASH("ItemOxite")
push HASH("ItemNitrice")
push HASH("ItemVolatiles")
push HASH("ItemCobaltOre")
push HASH("ItemCoalOre")
move OreIndex sp
push HASH("ItemIronOre")
push HASH("ItemCopperOre")
push HASH("ItemGoldOre")
push HASH("ItemSilverOre")
push HASH("ItemNickelOre")
push HASH("ItemLeadOre")
push HASH("ItemSiliconOre")
move IngotIndex sp

# Devices
alias VendingMachine d0
alias Sorter d1
alias Pause d2

# Initializations
s VendingMachine Lock 1
s Sorter Mode 2 # Logic
sbn Stacker ResourceOre Setting OreStackSize
sbn Stacker ResourceIngot Setting IngotStackSize

alias ItemHash  r2
alias ItemType  r3
alias ItemQuantity r4

main:
  l r15 VendingMachine Power
  beqz r15 main

  mod sp sp IngotIndex
  add sp sp 1
  peek ItemHash
  sle ItemType sp OreIndex

  # get capacity 
  sbn Switch ItemHash Open 0
  yield
  lbn ItemQuantity Memory ItemHash Setting Average
  lbn r15 Silo ItemHash ExportCount Average
  sub ItemQuantity ItemQuantity r15
  sbn Memory ItemHash Setting ItemQuantity
  sbn Silo ItemHash ClearMemory 1
  sbn Switch ItemHash Open 1

  # check capacity
  sge r15 ItemQuantity SiloMax
  breqz r15 2
  l r14 VendingMachine Ratio
  blt r14 PurgeThreshold main

vend:
  l r14 VendingMachine Power
  beqz r14 vend
  l r14 Pause Setting
  beqz r14 vend
  l r14 Sorter Output
  bneq r14 -1 vend

  # release item
  s VendingMachine RequestHash ItemHash
  ls r14 VendingMachine 1 Quantity
  beqz r14 main
  s Sorter Output r15
  bnez r15 main
  select r13 ItemType OreStackSize IngotStackSize
  div r14 r14 r13

  # update capacity
  add ItemQuantity ItemQuantity r14
  sbn Memory ItemHash Setting ItemQuantity
  bge ItemQuantity SiloMax main
  j vend