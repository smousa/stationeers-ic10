# Configuration
define MaxVolume 16000
define IdealGasConstant 8.314

# Structures
define GasSensor  HASH("StructureGasSensor")
define Stacker    HASH("StructureStacker")
define Door       HASH("StructureCompositeDoor")
define HeatPump   HASH("StructureVolumePump")
define HeatValve  HASH("StructureDigitalValve")

# Devices
alias Activator  d0 # Switch
alias IceMoles   d1 # Memory
alias Thermostat d2 # Memory
alias StackCount d3 # Dial

main:
  l r0 db NameHash
  l r1 db Setting
  l r2 Activator Setting
  l r3 StackCount Setting
  l r4 IceMoles Setting
  l r5 Thermostat Setting

  sbn Stacker r0 Mode 1
  sbn Door r0 Mode 1

  lbn r6 GasSensor r0 Pressure Average
  lbn r7 GasSensor r0 Temperature Average 
  lbn r8 Stacker r0 ExportCount Average 
  lbn r9 Stacker r0 Setting Average
  lbn r10 Door r0 Setting Maximum

  # Compute moles present
  mul r11 r6 MaxVolume
  div r11 r11 IdealGasConstant
  div r11 r11 r7
  select r11 r7 r11 0

  # Lock devices when activated
  snez r15 r2
  s StackCount Lock r15 
  sbn Stacker r0 Lock r15
  
  # Lock door when ice or gas present
  add r15 r11 r8
  sgtz r15 r15 
  sbn Door r0 Lock r15

  # Check for open door
  select r15 r15 0 r10
  sbn Door r0 Open r15 

  # Release stacker when activated
  beqz r2 reset 
  slt r14 r8 r3
  select r14 r14 1 -1
  select r15 r15 -1 r14
  sbn Stacker r0 Output r15 
  beq r15 1 done

  # Melt the ice
  slt r15 r7 r5
  mul r14 r3 r4
  mul r14 r14 r9
  add r14 r14 r1
  slt r14 r11 r14
  select r13 r14 r15 0
  seqz r12 r13 
  sbn HeatValve r0 On r13
  sbn HeatPump r0 On r12

  # Reset
  bnez r14 done
  s Activator Open 0

reset:
  s db Setting r11
  sbn Stacker r0 Output -1
  bgtz r8 done 
  sbn Stacker r0 ClearMemory 1

done:
  yield
  j main