# Configuration
define PollutantSpecificHeat     24.8
define CarbonDioxideSpecificHeat 28.2
define NitrogenSpecificHeat      20.6
define OxygenSpecificHeat        21.1
define WaterSpecificHeat         72

define MaxPressure             51675.75
define TargetRatio             0.33
define CondensationPressure    4512.6
define CondensationTemperature 350
define IdealGasConstant        8.314

# Structures
define GasSensor    HASH("StructureGasSensor")
define PipeAnalyzer HASH("StructurePipeAnalysizer")
define VolumePump   HASH("StructureVolumePump")
define Regulator    HASH("StructureBackPressureRegulator")

# Networks
define Icebox    HASH("Water Icebox")
define Pipe      HASH("WastePipeNetwork")

# Devices
alias FiltrationOutput db
alias FiltrationInput  d0
alias WasteThermostat  d1 # Memory
alias WaterThermostat  d2 # Memory
alias WaterMass        d3 # Memory

main:
  # Calculate specific heat
  l r0 FiltrationInput RatioPollutantOutput2
  l r1 FiltrationInput RatioCarbonDioxideOuput2
  l r2 FiltrationInput RatioNitrogenOutput2
  l r3 FiltrationInput RatioOxygenOutput2
  mul r15 r0 PollutantSpecificHeat
  mul r14 r1 CarbonDioxideSpecificHeat
  add r15 r15 r14
  mul r14 r2 NitrogenSpecificHeat
  add r15 r15 r14
  mul r14 r3 OxygenSpecificHeat
  add r15 r15 r14 # Specific Heat

  # Calculate Target Moles
  lbn r1 PipeAnalyzer Pipe Volume Average
  mul r14 r1 CondensationPressure
  div r14 r14 CondensationTemperature
  div r14 r14 IdealGasConstant # Target Moles
  sub r13 1 r0
  div r13 r14 r13 # Total Moles

  l r0 WasteThermostat Setting
  l r1 WaterThermostat Setting

  # Calculate Energy needed to reach target temperature
  l r2 FiltrationInput TemperatureOutput2
  sub r12 r2 r0
  mul r13 r13 r12
  mul r13 r13 r15 # Joules

  # Calculate Water needed to cool gas to target
  l r3 WaterMass Setting
  div r12 r13 WaterSpecificHeat
  sub r11 r1 r0
  div r12 r12 r11 
  max r12 r12 r3 # Water Moles
  s WaterVolume Setting r12

  # Input Pump Settings
  l r2 FiltrationInput TotalMolesOutput2
  lbn r3 PipeAnalyzer Pipe Pressure Average
  lbn r4 PipeAnalyzer Pipe RatioPollutant Average
  lbn r5 PipeAnalyzer Pipe RatioLiquidPollutant Average
  lbn r6 PipeAnalyzer Pipe TotalMoles Average
  lbn r7 GasSensor Icebox Temperature Average
  lbn r8 GasSensor Icebox RatioNitrogen Average
  lbn r9 GasSensor Icebox TotalMoles Average

  add r11 r4 r5 
  select r11 r6 r11 1 # Ratio [Liquid] Pollutant
  move r15 0
  beqz r2 flushPump
  bge r3 MaxPressure flushPump
  bgt r7 r1 flushPump
  sub r10 1 r11
  mul r10 r10 r6
  bge r10 r14 flushPump # Target Moles (Waste)
  sub r10 1 r8
  mul r10 r10 r9
  blt r10 r12 flushPump # Target Moles (Water)
  ceil r15 r11
flushPump:
  sbn VolumePump Pipe On r15

  # Output Regulator Settings
  l r2 FiltrationOutput PressureInput
  l r3 FiltrationOutput RatioCarbonDioxideInput
  lbn r4 PipeAnalyzer Pipe Temperature Average

  move r14 0
  bnez r15 flushRegulator
  bge r2 MaxPressure flushRegulator
  bge r3 TargetRatio flushRegulator
  bgt r4 r0 flushRegulator
  seqz r14 r11
flushRegulator:
  sbn Regulator WastePipeOutput On r14

  yield
  j main