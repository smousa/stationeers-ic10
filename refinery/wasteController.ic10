# Configuration
define PollutantSpecificHeat     24.8
define CarbonDioxideSpecificHeat 28.2
define NitrogenSpecificHeat      20.6
define OxygenSpecificHeat        21.1
define WaterSpecificHeat         72

define MaxPressure             51675.75
define CondensationPressure    4512.6
define CondensationTemperature 350
define IdealGasConstant        8.314

# Structures
define GasSensor    HASH("StructureGasSensor")
define PipeAnalyzer HASH("StructurePipeAnalysizer")
define VolumePump   HASH("StructureVolumePump")
define Regulator    HASH("StructureBackPressureRegulator")

# Networks
define Icebox    HASH("Water Icebox")
define Pipe      HASH("WastePipeNetwork")

# Devices
alias FiltrationOutput db
alias FiltrationInput  d0
alias WasteTemperature d1 # Memory
alias WaterTemperature d2 # Memory
alias WaterVolume      d3 # Memory

main:
  # Calculate specific heat
  l r0 FiltrationInput RatioPollutantOutput2
  l r1 FiltrationInput RatioCarbonDioxideOuput2
  l r2 FiltrationInput RatioNitrogenOutput2
  l r3 FiltrationInput RatioOxygenOutput2
  mul r15 r0 PollutantSpecificHeat
  mul r14 r1 CarbonDioxideSpecificHeat
  add r15 r15 r14
  mul r14 r2 NitrogenSpecificHeat
  add r15 r15 r14
  mul r14 r3 OxygenSpecificHeat
  add r15 r15 r14 # Specific Heat

  # Calculate Target Moles
  lbn r1 PipeAnalyzer Pipe Volume Average
  mul r14 r1 CondensationPressure
  div r14 r14 CondensationTemperature
  div r14 r14 IdealGasConstant # Target Moles

  # Calculate Energy needed to reach target temperature
  l r1 FiltrationInput TemperatureOutput2
  l r2 WasteTemperature Setting
  sub r13 1 r0
  div r13 r14 r13 # Total Moles
  sub r12 r1 r2
  mul r13 r13 r12
  mul r13 r13 r15 # Joules

  # Calculate Water needed to cool gas to target
  l r3 WaterTemperature Setting
  l r4 WaterVolume Setting
  div r12 r13 WaterSpecificHeat
  sub r11 r2 r3
  div r12 r12 r11 
  max r12 r12 r4 # Water Moles
  s WaterVolume Setting r12

  # Input Pump Settings
  l r0 FiltrationInput TotalMolesOutput2
  lbn r1 PipeAnalyzer Pipe Pressure Average
  lbn r2 PipeAnalyzer Pipe RatioPollutant Average
  lbn r3 PipeAnalyzer Pipe RatioLiquidPollutant Average
  lbn r4 PipeAnalyzer Pipe TotalMoles Average
  lbn r5 GasSensor Icebox Temperature Average
  lbn r6 GasSensor Icebox RatioNitrogen Average
  lbn r7 GasSensor Icebox TotalMoles Average

  add r11 r2 r3 
  select r11 r4 r11 1 # Ratio [Liquid] Pollutant
  move r15 0
  beqz r0 flushPump
  bge r1 MaxPressure flushPump
  bgt r4 MinTemperature flushPump
  sub r10 1 r11
  mul r10 r10 r4
  bge r10 r14 flushPump # Target Moles (Waste)
  sub r10 1 r6
  mul r10 r10 r7
  blt r10 r12 flushPump # Target Moles (Water)
  ceil r15 r11
flushPump:
  sbn VolumePump Pipe On r15

  # Output Regulator Settings
  l r0 FiltrationOutput PressureInput
  lbn r1 PipeAnalyzer Temperature Average

  move r14 0
  bge r0 MaxPressure flushRegulator
  bgt r1 MaxTemperature flushRegulator
  bnez r11 flushRegulator
  seqz r14 r15
flushRegulator:
  sbn Regulator WastePipeOutput On r14

  yield
  j main
