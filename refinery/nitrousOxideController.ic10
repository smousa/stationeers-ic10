# Configuration
define MaxPressure 51675.75
define NitrousOxideMaxPressure 680
define TargetRatio 0.33

# Structures
define GasSensor    HASH("StructureGasSensor")
define PipeAnalyzer HASH("StructurePipeAnalysizer")
define ActiveVent   HASH("StructureActiveVent")
define Regulator    HASH("StructureBackPressureRegulator")
define CoolPump     HASH("StructureVolumePump")
define CoolValve    HASH("StructureDigitalValve")

# Networks
define Icebox       HASH("Nitrice Icebox")
define Pipe         HASH("NitrousOxidePipeNetwork")
define WastePipe    HASH("NitricePipeNetwork")

# Devices
alias FiltrationInput  db
alias FiltrationOutput d0
alias Activator        d1 # Switch
alias Thermostat       d2 # Memory

main:
  l r0 Activator Setting
  l r1 Thermostat Setting
  lbn r2 GasSensor Icebox RatioNitrousOxide Average
  lbn r3 GasSensor Icebox RatioNitrogen Average
  lbn r4 GasSensor Icebox Pressure Average
  l r5 FiltrationInput PressureOutput
  l r6 FiltrationInput TemperatureOutput
  lbn r7 PipeAnalyzer WastePipe Pressure Average
  lbn r8 PipeAnalyzer WastePipe RatioNitrogen Average
  l r9 FiltrationOutput Pressure
  l r10 FiltrationOutput RatioNitrogen

  # Pipe Vent
  move r15 0
  bnez r0 flushPipeVent
  bge r5 NitrousOxideMaxPressure flushPipeVent
  floor r15 r2
flushPipeVent:
  sbn ActiveVent Pipe Mode 1 # Inward
  sbn ActiveVent Pipe PressureInternal NitrousOxideMaxPressure
  sbn ActiveVent Pipe On r15

  # Pipe Cooler
  sgt r14 r6 r1
  seqz r13 r14
  sbn CoolValve Pipe On r14
  sbn CoolPump Pipe On r13

  # Waste Vent
  move r14 0
  bnez r0 flushWasteVent
  bgt r7 MaxPressure flushWasteVent
  ceil r14 r3
flushWasteVent:
  sbn ActiveVent Pipe Mode 1 # Inward
  sbn ActiveVent Pipe PressureInternal MaxPressure
  sbn ActiveVent Pipe On r14

  # Waste Regulator
  move r13 0
  bnez r14 flushWasteRegulator
  bgt r9 MaxPressure flushWasteRegulator
  bgt r10 TargetRatio flushWasteRegulator
  floor r13 r8
flushWasteRegulator:
  sbn Regulator WastePipe On r13

  # Activator
  seqz r12 r0
  s Activator Lock r12
  bnez r0 done
  bnez r15 done
  bnez r14 done
  bnez r4 done
  s Activator Open 1
  
done:
  yield
  j main
