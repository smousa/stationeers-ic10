# Configuration 
define MaxPressure 51675.75

# Structures 
define ActiveVent HASH("StructureActiveVent")
define GasSensor HASH("StructureGasSensor")

# Networks
define VolatilesIcebox HASH("Volatiles Icebox")
define VolatilesPipe HASH("VolatilesPipeNetwork")
define OxiteIcebox HASH("Oxite Icebox")
define OxitePipe HASH("OxitePipeNetwork")

# Devices
alias Filtration db # IC Housing 
alias Volatiles  d0 # IC Housing
alias Oxite      d1 # IC Housing

sbn ActiveVent VolatilesPipe Mode 1
sbn ActiveVent VolatilesPipe PressureInternal MaxPressure 
sbn ActiveVent OxitePipe Mode 1
sbn ActiveVent OxitePipe PressureInternal MaxPressure 

main:
  move r15 0
  # Check filter life
  ls r0 Filtration 0 Quantity
  ls r1 Filtration 1 Quantity
  add r14 r0 r1
  beqz r14 flushFiltration

  # Check pipe pressure 
  l r0 Filtration PressureInput
  l r1 Filtration RatioVolatilesInput
  l r2 Filtration PressureOutput
  l r3 Filtration PressureOutput2
  beqz r0 flushFiltration
  select r2 r1 r2 0
  bge r2 r0 flushFiltration
  bge r3 r0 flushFiltration 
  move r15 1

flushFiltration:
  s db Mode r15

  push VolatilesIcebox
  push VolatilesPipe
  push 0
  jal ventilate

  push OxiteIcebox
  push OxitePipe
  push 1
  jal ventilate

  yield
  j main

ventilate:
  pop r0
  pop r1
  pop r2

  alias Control dr0
  alias Icebox r1
  alias Pipe r2

  l r3 Control Setting
  lbn r4 GasSensor Icebox TotalMoles Average
  select r15 r3 0 r4
  sgtz r15 r15
  sbn ActiveVent Pipe On r15
  bgtz r3 ra
  bgtz r4 ra
  s Control Setting 1
  j ra