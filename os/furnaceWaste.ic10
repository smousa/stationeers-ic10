# Configuration
define MaxPressure 51675.75
define MaxTemperature 303.15
define CondensationTemperature 431
define CondensationPressure 6000

# Structures
define Pump HASH("StructureVolumeGasPump")
define Valve HASH("StructureDigitalValve")
define PurgeValve HASH("StructurePurgeValve")
define PipeAnalyzer HASH("StructurePipeAnalysizer")

# Networks
define CoolantPipe HASH("CoolantPipeNetwork")
define WastePipe HASH("WastePipeNetwork")

alias Filtration db
alias OutputFiltration d1

main:
  # Check pipe temperature
  l r0 Filtration TemperatureOutput2
  sgt r15 r0 MaxTemperature
  seqz r14 r15 
  sbn Pump CoolantPipe On r15
  sbn Valve CoolantPipe On r14

  move r15 0
  # Check filter life
  ls r1 Filtration 0 Quantity
  beqz r1 flushFiltration
  ls r1 Filtration 1 Quantity
  beqz r1 flushFiltration

  # Check input pressure and combustion 
  l r1 Filtration CombustionInput
  bnez r1 flushFiltration
  l r1 Filtration PressureInput 
  beqz r1 flushFiltration 

  # Check output pressure
  l r1 Filtration PressureOutput2
  bgt r1 MaxPressure flushFiltration

  # Compute moles needed to force condensation
  l r3 Filtration TotalMolesOutput2
  l r4 Filtration RatioPollutantOutput2
  l r5 Filtration RatioLiquidPollutantOutput2

  mul r14 r0 r3
  div r14 r14 r1
  snan r15 r14
  bnez r15 flushFiltration
  mul r14 r14 CondensationPressure
  div r14 r14 CondensationTemperature # moles

  add r13 r4 r5
  sub r13 1 r13 # not X
  ceil r15 r13
  bnez r15 flushFiltration

  mul r13 r13 r3
  sle r15 r13 r14

flushFiltration:
  s Filtration Mode r15 

yield
j main