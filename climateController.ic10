# ON/OFF
define TRUE 1
define FALSE 0

# Inward/Outward
define INWARD 1
define OUTWARD 0

# Vents
define ActiveVent        HASH("StructureActiveVent")
define AirVent           HASH("AirVent")
define OxygenVent        HASH("OxygenVent")
define CarbonDioxideVent HASH("CarbonDioxideVent")
define NitrogenVent      HASH("NitrogenVent")

# Sensor
define GasSensor          HASH("StructureGasSensor")
define Sensor             HASH("InteriorGasSensor")

# Tolerances
define Dial                      HASH("StructureLogicDial")
define PressureToleranceMin      HASH("PressureToleranceMin")
define PressureToleranceMax      HASH("PressureToleranceMax")
define OxygenToleranceMin        HASH("OxygenToleranceMin")  
define OxygenToleranceMax        HASH("OxygenToleranceMax") 
define CarbonDioxideToleranceMin HASH("CarbonDioxideToleranceMin")
define CarbonDioxideToleranceMax HASH("CarbonDioxideToleranceMax")
define PollutantToleranceMin     HASH("PollutantToleranceMin")
define PollutantToleranceMax     HASH("PollutantToleranceMax")

# Atmosphere Settings
define AtmospherePressureLow       0
define AtmospherePressureHigh      1
define AtmosphereOxygenLow         2
define AtmosphereOxygenHigh        3
define AtmosphereCarbonDioxideLow  4
define AtmosphereCarbonDioxideHigh 5
define AtmospherePollutantLow      6
define AtmospherePollutantHigh     7

alias Atmosphere r0

# Masks
define AirMask           $02 # PressureHigh
define OxygenMask        $A5 # PollutantHigh, CarbonDioxideHigh, OxygenLow, PressureLow
define CarbonDioxideMask $10 # CarbonDioxideLow
define NitrogenMask      $08 # OxygenHigh

# Initialize Vents
sb AirVent Lock TRUE
sb AirVent On   FALSE
sb AirVent Mode INWARD

sb OxygenVent Lock TRUE
sb OxygenVent On   FALSE
sb OxygenVent Mode OUTWARD

sb CarbonDioxideVent Lock TRUE
sb CarbonDioxideVent On   FALSE
sb CarbonDioxideVent Mode OUTWARD

sb NitrogenVent Lock TRUE
sb NitrogenVent On   FALSE
sb NitrogenVent Mode OUTWARD

## External pressure
lbn r15 Dial PressureToleranceMin Setting Average
sbn ActiveVent AirVent PressureExternal r15

lbn r15 Dial PressureToleranceMax Setting Average
sbn ActiveVent OxygenVent PressureExternal r15
sbn ActiveVent CarbonDioxideVent PressureExternal r15
sbn ActiveVent NitrogenVent PressureExternal r15

# Begin program execution
main:
  # Clear registers
  move Atmosphere 0

  # Get the pressure, min, max
  lbn r15 GasSensor Sensor Pressure Average

  ## Pressure Min
  lbn r14 Dial PressureToleranceMin Setting Average
  slt r13 r15 r14
  sll r12 r13 AtmospherePressureLow
  or Atmosphere Atmosphere r12

  ## Pressure Max
  lbn r14 Dial PressureToleranceMax Setting Average
  sge r13 r15 r14
  sll r12 r13 AtmospherePressureHigh
  or Atmosphere Atmosphere r12
  
  # Get oxygen ratio, min, max
  lbn r15 GasSensor Sensor RatioOxygen Average

  ## Oxygen Min
  lbn r14 Dial OxygenToleranceMin Setting Average
  slt r13 r15 r14
  sll r12 r13 AtmosphereOxygenLow
  or Atmosphere Atmosphere r12

  ## Oxygen Max
  lbn r14 Dial OxygenToleranceMax Setting Average
  sge r13 r15 r14
  sll r12 r13 AtmosphereOxygenHigh
  or Atmosphere Atmosphere r12

  # Get carbon dioxide ratio, min, max
  lbn r15 GasSensor Sensor RatioCarbonDioxide Average

  ## Carbon Dioxide Min
  lbn r14 Dial CarbonDioxideToleranceMin Setting Average
  slt r13 r15 r14
  sll r12 r13 AtmosphereCarbonDioxideLow
  or Atmosphere Atmosphere r12

  ## Carbon Dioxide Max
  lbn r14 Dial CarbonDioxideToleranceMax Setting Average
  sge r13 r15 r14
  sll r12 r13 AtmosphereCarbonDioxideHigh
  or Atmosphere Atmosphere r12

  # Get pollutant ratio, min, max
  lbn r15 GasSensor Sensor RatioPollutant Average
  
  ## Pollutant Min
  lbn r14 Dial PollutantToleranceMin Setting Average
  slt r13 r15 r14
  sll r12 r13 AtmospherePollutantLow
  or Atmosphere Atmosphere r12

  ## Pollutant Max
  lbn r14 Dial PollutantToleranceMax Setting Average
  sge r13 r15 r14
  sll r12 r13 AtmospherePollutantHigh
  or Atmosphere Atmosphere r12

  # Compute state for Air
  and r15 Atmosphere AirMask
  sgtz r14 r15
  sbn ActiveVent AirVent On r14

  # Compute state for Oxygen
  and r15 Atmosphere OxygenMask
  sgtz r14 r15
  sbn ActiveVent OxygenVent On r14

  # Compute state for Carbon Dioxide
  and r15 Atmosphere CarbonDioxideMask
  sgtz r14 r15
  sbn ActiveVent CarbonDioxideVent On r14

  # Compute state for Nitrogen
  and r15 Atmosphere NitrogenMask
  sgtz r14 r15
  sbn ActiveVent NitrogenVent On r14
   
  yield
  j main
