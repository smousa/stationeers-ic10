define TRUE 1
define FALSE 0

# Inward/Outward
define INWARD 1
define OUTWARD 0

define PRESSURE_TOLERANCE_MIN 0.99
define PRESSURE_TOLERANCE_MAX 1.01

# Components
define GasSensor    HASH("StructureGasSensor")
define ActiveVent   HASH("StructureActiveVent")
define DigitalValve HASH("StructureDigitalValve")

# Inputs
alias Thermostat         d0
alias Pressure           d1
alias RatioOxygen        d2
alias RatioCarbonDioxide d3

define RoomGasSensor     HASH("RoomGasSensor")
define AirConditioner    HASH("AirConditioner")
define ExhaustVent       HASH("ExhaustVent")
define OxygenVent        HASH("OxygenVent")
define CarbonDioxideVent HASH("CarbonDioxideVent")
define NitrogenVent      HASH("NitrogenVent")

# Initialize Air Conditioner
sbn DigitalValve AirConditioner Lock TRUE
sbn DigitalValve AirConditioner On   FALSE

# Initialize Vents
sbn ActiveVent ExhaustVent Lock TRUE
sbn ActiveVent ExhaustVent On   FALSE
sbn ActiveVent ExhaustVent Mode INWARD

sbn ActiveVent OxygenVent Lock TRUE
sbn ActiveVent OxygenVent On   FALSE
sbn ActiveVent OxygenVent Mode OUTWARD

sbn ActiveVent CarbonDioxideVent Lock TRUE
sbn ActiveVent CarbonDioxideVent On   FALSE
sbn ActiveVent CarbonDioxideVent Mode OUTWARD

sbn ActiveVent NitrogenVent Lock TRUE
sbn ActiveVent NitrogenVent On   FALSE
sbn ActiveVent NitrogenVent Mode OUTWARD

# Begin program execution
main:
  # Check the temperature
  l r0 Thermostat Setting
  lbn r1 GasSensor RoomGasSensor Temperature Average
  sgt r2 r1 r0
  sbn DigitalValve AirConditioner On r2

  # Configure Vents
  l r0 Pressure Setting

  mul r1 r0 PRESSURE_TOLERANCE_MIN
  sbn ActiveVent ExhaustVent PressureExternal r1

  mul r1 r0 PRESSURE_TOLERANCE_MAX
  sbn ActiveVent OxygenVent PressureExternal r1
  sbn ActiveVent CarbonDioxideVent PressureExternal r1
  sbn ActiveVent NitrogenVent PressureExternal r1

  # Check the Pressure
  lbn r1 GasSensor RoomGasSensor Pressure Average
  sgt r2 r1 r0
  sbn ActiveVent ExhaustVent On r2
  slt r2 r1 r0
  sbn ActiveVent NitrogenVent On r2

  # Maintain Oxygen Levels
  l r0 RatioOxygen Setting
  mul r0 r0 0.01
  lbn r1 GasSensor RoomGasSensor RatioOxygen Average
  slt r2 r1 r0
  sbn ActiveVent OxygenVent On r2

  # Maintain Carbon Dioxide Levels
  l r0 RatioCarbonDioxide Setting
  mul r0 r0 0.01
  lbn r1 GasSensor RoomGasSensor RatioCarbonDioxide Average
  slt r2 r1 r0
  sbn ActiveVent CarbonDioxideVent On r2

  yield
  j main
