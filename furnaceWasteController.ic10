# Configuration
define MaxPressure 51675.75
define WasteTemperature 303
define WasteTemperatureMax 313
define WasteTemperatureMin 293
# Properties
define PollutantCondensationTemperature 425    # K
define PollutantCondensationPressure 6000      # kPa
define PollutantLatentHeat 2000                # J/mol
define OxygenSpecificHeat 21.1                 # J/(K*mol)
define NitrogenSpecificHeat 20.5               # J/(K*mol)
define CarbonDioxideSpecificHeat 28.2          # J/(K*mol)
# Structures
define VolumePump   HASH("StructureVolumePump")
define PurgeValve   HASH("StructurePurgeValve")
# Devices
define Out1 HASH("CombustiblePipeNetwork")
define Out2 HASH("PollutantPipeNetwork")

main:
  move r15 0
checkFilterSlot0:
  ls r2 db 0 OccupantHash
  ls r3 db 0 Quantity
  beqz r3 flushFiltration
  beq r2 HASH("ItemGasFilterNitrousOxide") checkFilterSlot1
  beq r2 HASH("ItemGasFilterNitrousOxideM") checkFilterSlot1
  beq r2 HASH("ItemGasFilterNitrousOxideL") checkFilterSlot1
  beq r2 HASH("ItemGasFilterNitrousOxideInfinite") checkFilterSlot1
  j flushFiltration
checkFilterSlot1:
  ls r2 db 1 OccupantHash
  ls r3 db 1 Quantity
  beqz r3 flushFiltration
  beq r2 HASH("ItemGasFilterVolatiles") checkPipes
  beq r2 HASH("ItemGasFilterVolatilesM") checkPipes
  beq r2 HASH("ItemGasFilterVolatilesL") checkPipes
  beq r2 HASH("ItemGasFilterVolatilesInfinite") checkPipes
  j flushFiltration
checkPipes:
  l r1 db PressureInput
  l r2 db CombustionInput
  l r3 db PressureOutput
  l r4 db PressureOutput2
  l r5 db TemperatureOutput2
  l r6 db RatioPollutantOutput2
  beqz r1 flushFiltration
  bnez r2 flushFiltration
  bge r3 MaxPressure flushFiltration
  bge r4 MaxPressure flushFiltration
  mul r14 r4 r6
  bgt r14 PollutantCondensationPressure flushFiltration
  slt r14 r5 PollutantCondensationTemperature
  sge r13 r5 WasteTemperatureMin
  beq r14 r13 flushFiltration
  move r15 1
flushFiltration:
  s db Mode r15
checkOutput:
  l r0 db PressureOutput
  l r1 db TemperatureOutput
  l r2 db RatioVolatilesOutput
  l r3 db RatioNitrousOxideOutput
  l r4 db RatioLiquidNitrousOxideOutput
  sge r15 r0 MaxPressure
  bnez r15 flushOutputVolumePump
  beqz r1 flushOutputVolumePump
  bgtz r2 flushOutputVolumePump
  bgtz r3 flushOutputVolumePump
  bgtz r4 flushOutputVolumePump
  move r15 1
flushOutputVolumePump:
  sbn VolumePump Out1 On r15
  sgtz r15 r2
  sbn PurgeValve Out1 On r15
checkOutput2:
  l r1 db TemperatureOutput2
  l r2 db RatioOxygenOutput2
  l r3 db RatioNitrogenOutput2
  l r4 db RatioCarbonDioxideOutput2
  l r5 db RatioPollutantOutput2
  l r6 db RatioLiquidPollutantOutput2
  move r15 0
  bgt r1 WasteTemperature flushOutput2VolumePump
  beqz r1 flushOutput2VolumePump
  bgtz r5 flushOutput2VolumePump
  bgtz r6 flushOutput2VolumePump
  move r15 1
flushOutput2VolumePump:
  sbn VolumePump Out2 On r15
  move r14 WasteTemperatureMax
  add r13 r2 r3
  add r13 r13 r4
  mul r13 r13 r5
  mul r13 r13 PollutantLatentHeat
  beqz r13 checkTargetTemperature
  mul r12 r2 OxygenSpecificHeat
  mul r11 r3 NitrogenSpecificHeat
  add r12 r12 r11
  mul r11 r4 CarbonDioxideSpecificHeat
  add r12 r12 r11
  div r13 r13 r12
  add r14 r14 r13
checkTargetTemperature:
  sgt r15 r1 r14
  sbn PurgeValve Out2 On r15
yield
j main
