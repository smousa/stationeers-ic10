# ON/OFF
define OFF 0
define ON 1
# ACTIVE/IDLE
define IDLE 0
define ACTIVE 1
# Configuration
define MaxT 303
define MinT 293
define MaxP 51675.75
define PolCndT 425
define PolCndP 6000
# Structures
define VolumePump HASH("StructureVolumePump")
define PurgeValve HASH("StructurePurgeValve")
# Device Classes
define Out1 HASH("CombustiblePipeNetwork")
define Out2   HASH("PollutantPipeNetwork")
# Slot Filters
define FilterN2O HASH("ItemGasFilterNitrousOxide")
define FilterN2OM HASH("ItemGasFilterNitrousOxideM")
define FilterN2OL HASH("ItemGasFilterNitrousOxideL")
define FilterN2OInf HASH("ItemGasFilterNitrousOxideInfinite")
define FilterVol HASH("ItemGasFilterVolatiles")
define FilterVolM HASH("ItemGasFilterVolatilesM")
define FilterVolL HASH("ItemGasFilterVolatilesL")
define FilterVolInf HASH("ItemGasFilterVolatilesInfinite")
# Initialize devices
s db Mode IDLE
sbn VolumePump Out1 On OFF
sbn PurgeValve Out1 On OFF
sbn VolumePump Out2 On OFF
sbn PurgeValve Out2 On OFF
main:
  jal setFiltration
  jal setOutputNetwork
  jal setOutput2Network
  yield
  j main
setFiltration:
checkSlot0:
  ls r0 db 0 Occupied
  beqz r0 filtrationIdle
  ls r0 db 0 OccupantHash
  beq r0 FilterN2OInf checkSlot1
  ls r1 db 0 Quantity
  beqz r1 filtrationIdle
  # nitrous oxide
  seq r1 r0 FilterN2O
  seq r2 r0 FilterN2OM
  or r1 r1 r2
  seq r2 r0 FilterN2OL
  or r1 r1 r2
  beqz r1 filtrationIdle
checkSlot1:
  ls r0 db 1 Occupied
  beqz r0 filtrationIdle
  ls r0 db 1 OccupantHash
  beq r0 FilterVolInf checkInput
  ls r1 db 1 Quantity
  beqz r1 filtrationIdle
  # volatile
  seq r1 r0 FilterVol
  seq r2 r0 FilterVolM
  or r1 r1 r2
  seq r2 r0 FilterVolL
  or r1 r1 r2
  beqz r1 filtrationIdle
checkInput:
  l r0 db PressureInput
  beqz r0 filtrationIdle
  l r0 db CombustionInput
  bnez r0 filtrationIdle
checkOutput:
  l r0 db PressureOutput
  bge r0 MaxP filtrationIdle
checkOutput2:
  l r0 db PressureOutput2
  bge r0 MaxP filtrationIdle
  l r1 db RatioPollutantOutput2
  mul r1 r1 r0
  bgt r1 PolCndP filtrationIdle
  l r0 db TemperatureOutput2
  slt r1 r0 MinT
  sgt r2 r0 PolCndT
  or r1 r1 r2
  beqz r1 filtrationIdle
  s db Mode ACTIVE
  j ra
filtrationIdle:
  s db Mode IDLE
  j ra
setOutputNetwork:
  l r0 db PressureOutput
  l r1 db RatioNitrousOxideOutput
  l r2 db RatioVolatilesOutput
  sgtz r3 r0
  add r4 r1 r2
  seqz r4 r4
  and r3 r3 r4
  sbn VolumePump Out1 On r3
  sgtz r3 r2
  sbn PurgeValve Out1 On r3
  j ra
setOutput2Network:
  l r0 db PressureOutput2
  l r1 db TemperatureOutput2
  l r2 db RatioPollutantOutput2
  sgtz r3 r0
  seqz r4 r2
  and r3 r3 r4
  sle r4 r1 MaxT
  and r3 r3 r4
  sbn VolumePump Out2 On r3
  sle r3 r1 PolCndT
  sgt r4 r1 MaxT
  and r3 r3 r4
  slt r4 r1 MinT
  or r3 r3 r4
  sbn PurgeValve Out2 On r3
  j ra
