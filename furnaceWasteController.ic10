# Configuration
define MaxPressure 51675.75
define WasteTemperature 303
define WasteTemperatureMax 313
define WasteTemperatureMin 293
# Properties
define PollutantCondensationTemperature 425    # K
define PollutantCondensationPressure 6000      # kPa
define PollutantLatentHeat 2000                # J/mol
define OxygenSpecificHeat 21.1                 # J/(K*mol)
define NitrogenSpecificHeat 20.5               # J/(K*mol)
define CarbonDioxideSpecificHeat 28.2          # J/(K*mol)
# Structures
define VolumePump   HASH("StructureVolumePump")
define PurgeValve   HASH("StructurePurgeValve")
# Devices
define Out1 HASH("CombustiblePipeNetwork")
define Out2 HASH("PollutantPipeNetwork")

alias TargetTemperature r0
s db Mode 0

main:
computeTargetTemperature:
  l r1 db RatioOxygenOutput2 
  l r2 db RatioNitrogenOutput2
  l r3 db RatioCarbonDioxideOutput2 
  l r4 db RatioPollutantOutput2
  move TargetTemperature WasteTemperatureMax
  add r15 r1 r2
  add r15 r15 r3
  mul r15 r15 r4
  mul r15 r15 PollutantLatentHeat
  beqz r15 checkFilterSlot0
  mul r14 r1 OxygenSpecificHeat
  mul r13 r2 NitrogenSpecificHeat
  add r14 r14 r13
  mul r13 r3 CarbonDioxideSpecificHeat
  add r14 r14 r13 
  div r15 r15 r14
  add TargetTemperature TargetTemperature r15
checkFilterSlot0:
  ls r2 db 0 OccupantHash
  ls r3 db 0 Quantity
  move r15 0
  beqz r3 flushFiltration
  beq r2 HASH("ItemGasFilterNitrousOxide") checkFilterSlot1
  beq r2 HASH("ItemGasFilterNitrousOxideM") checkFilterSlot1
  beq r2 HASH("ItemGasFilterNitrousOxideL") checkFilterSlot1
  beq r2 HASH("ItemGasFilterNitrousOxideInfinite") checkFilterSlot1
  j flushFiltration
checkFilterSlot1:
  ls r2 db 1 OccupantHash
  ls r3 db 1 Quantity
  beqz r3 flushFiltration
  beq r2 HASH("ItemGasFilterVolatiles") checkPipes
  beq r2 HASH("ItemGasFilterVolatilesM") checkPipes
  beq r2 HASH("ItemGasFilterVolatilesL") checkPipes
  beq r2 HASH("ItemGasFilterVolatilesInfinite") checkPipes
  j flushFiltration
checkPipes:
  l r1 db PressureInput
  l r2 db CombustionInput
  l r3 db PressureOutput
  l r4 db PressureOutput2
  l r5 db TemperatureOutput2
  l r6 db RatioPollutantOutput2
  beqz r1 flushFiltration
  bnez r2 flushFiltration
  bge r3 MaxPressure flushFiltration
  bge r4 MaxPressure flushFiltration
  mul r14 r4 r6
  bgt r14 PollutantCondensationPressure flushFiltration
  move r15 1
  blt r5 WasteTemperatureMin flushFiltration
  bge r5 PollutantCondensationTemperature flushFiltration
  j checkOutput
flushFiltration:
  s db Mode r15
checkOutput:
  l r1 db TemperatureOutput
  l r2 db RatioVolatilesOutput
  l r3 db RatioNitrousOxideOutput
  l r4 db RatioLiquidNitrousOxideOutput
  move r15 0
  beqz r1 flushOutput
  bgtz r2 flushOutput
  bgtz r3 flushOutput
  bgtz r4 flushOutput
  move r15 1
flushOutput:
  sbn VolumePump Out1 On r15
  snez r15 r2
  sbn PurgeValve Out1 On r15
checkOutput2:
  l r1 db TemperatureOutput2
  l r2 db RatioPollutantOutput2
  l r3 db RatioLiquidPollutantOutput2
  move r15 0
  beqz r1 flushOutput2
  bgt r1 WasteTemperature flushOutput2 
  bgtz r2 flushOutput2
  bgtz r3 flushOutput2
  move r15 1
flushOutput2:
  sbn VolumePump Out2 On r15
  sgt r15 r1 TargetTemperature
  sbn PurgeValve Out2 On r15
yield
j main
