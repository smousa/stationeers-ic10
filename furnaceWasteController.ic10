# Configuration
define MaxPressure 51765.75
define WasteTemperature 308
define WasteTemperatureMin 261.6

# Properties
define PollutantCondensationTemperature 425 # K
define PollutantCondensationPressure 6000   # kPa
define PollutantLatentHeat 2000             # J/mol
define OxygenSpecificHeat 21.1              # J/(K*mol)
define NitrogenSpecificHeat 20.5            # J/(K*mol)
define CarbonDioxideSpecificHeat 28.2       # J/(K*mol)

# Structures
define VolumePump HASH("StructureVolumePump")
define PurgeValve HASH("StructurePurgeValve")

# Networks
define Out1 HASH("CombustiblePipeNetwork")
define Out2 HASH("PollutantPipeNetwork")

# Devices
alias SourcePipe db

main:
  # Check filter life
  move r15 0
  ls r14 SourcePipe 0 Quantity
  beqz r14 flushFiltration
  ls r14 SourcePipe 1 Quantity
  beqz r14 flushFiltration

  # Check input pipe
  l r14 SourcePipe PressureInput
  beqz r14 flushFiltration
  l r14 SourcePipe CombustionInput
  bnez r14 flushFiltration

  # Check output pipe
  l r14 SourcePipe PressureOutput
  bge r14 MaxPressure flushFiltration

  # Check waste pipe
  l r14 SourcePipe PressureOutput2
  l r13 SourcePipe TemperatureOutput2
  l r12 SourcePipe RatioPollutantOutput2
  bge r14 MaxPressure flushFiltration
  mul r11 r14 r12
  bgt r11 PollutantCondensationPressure flushFiltration
  move r15 1
  ble r13 WasteTemperatureMin flushFiltration
  bge r13 PollutantCondensationTemperature flushFiltration
  j checkOutput

flushFiltration:
  s SourcePipe Mode r15

checkOutput:
  l r14 SourcePipe TemperatureOutput
  l r13 SourcePipe RatioVolatilesOutput
  l r12 SourcePipe RatioNitrousOxideOutput
  l r11 SourcePipe RatioLiquidNitrousOxideOutput
  move r15 0
  beqz r14 flushOutput
  bnez r13 flushOutput
  bnez r12 flushOutput
  bnez r11 flushOutput
  move r15 1

flushOutput:
  sbn VolumePump Out1 On r15
  snez r15 r13
  sbn PurgeValve Out1 On r15

  alias TargetTemperature r0
  alias TargetVolume r1
  alias cSourcePipe r2

  # Compute specific heat of the source pipe (o2,n2,co2)
  l r5 SourcePipe RatioOxygenOutput2
  l r6 SourcePipe RatioNitrogenOutput2
  l r7 SourcePipe RatioCarbonDioxideOutput2
  mul cSourcePipe r5 OxygenSpecificHeat
  mul r15 r6 NitrogenSpecificHeat
  add cSourcePipe cSourcePipe r15
  mul r15 r7 CarbonDioxideSpecificHeat
  add cSourcePipe cSourcePipe r15
  add r15 r5 r6
  add r15 r15 r7
  div cSourcePipe cSourcePipe r15

  # Initialize Values
  l r5 SourcePipe RatioPollutantOutput2
  l r6 SourcePipe RatioLiquidPollutantOutput2
  l r7 SourcePipe TemperatureOutput2
  l r8 SourcePipe PressureOutput2
  lbn r9 VolumePump Out2 Maximum Minimum

  # Compute target temperature of the waste pipe
  move TargetTemperature WasteTemperature
  add r13 r5 r6
  bnan cSourcePipe flushOutput2
  mul r13 r13 PollutantLatentHeat
  div r13 r13 cSourcePipe
  add TargetTemperature TargetTemperature r13

flushOutput2:
  sle r12 r7 TargetTemperature
  select r14 r13 0 r12
  select TargetVolume r12 10 0
  seqz r15 TargetVolume

  sbn PurgeValve Out2 On r15
  sbn VolumePump Out2 On r14
  sbn VolumePump Out2 Setting TargetVolume

yield
j main
