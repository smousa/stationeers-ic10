# Configuration
define MaxCapacity 0.85
define MaxStacks 1740           # 2048 * 0.85
define MaxStackSize 50
define MaxPressure 51675.75     # 60795 kPa * 0.85

define MinStillPressure 20      # kPa
define MaxStillPressure 100     # kPa
define LiquidMolesPerLiter 55.6
define LiquidMolesPerUnit 20
define GasMolesPerUnit 5
define TargetTemperature 303    # K

define IdealGasConstant 8.314

# Structures
define DaylightSensor HASH("StructureDaylightSensor")
define GasSensor HASH("StructureGasSensor")
define PipeAnalyzer HASH("StructurePipeAnalysizer")
define ActiveVent HASH("StructureActiveVent")
define VolumePump HASH("StructureVolumePump")
define LiquidDigitalValve HASH("StructureLiquidDigitalValve")
define Stacker HASH("StructureStacker")
define LiquidTank HASH("StructureLiquidTankBigInsulated")
define GasTank HASH("StructureTankSmallInsulated")

# Networks
define Still HASH("WaterStillNetwork")

main:
  lbn r0 Stacker Still ExportCount Sum
  bnez r0 monitor

  lbn r1 LiquidTank Still VolumeOfLiquid Sum
  lbn r2 LiquidTank Still Volume Sum
  mul r2 r2 MaxCapacity
  sub r15 r2 r1
  mul r15 r15 LiquidMolesPerLiter
  div r15 r15 LiquidMolesPerUnit
  lbn r1 GasTank Still Volume Sum
  lbn r2 GasTank Still TotalMoles Sum
  mul r14 r1 MaxPressure
  div r14 r14 IdealGasConstant
  div r14 r14 TargetTemperature
  div r14 r14 GasMolesPerUnit
  min r15 r15 r14
  div r15 r15 MaxStacks
  min r15 r15 MaxStackSize
  floor r15 r15
  blez r15 done
  sbn Stacker Still Setting r15

monitor:
  sge r15 r0 MaxStacks # isFull

  seqz r14 r15
  s db Setting r14

  lb r1 DaylightSensor SolarIrradiance Average
  snez r14 r1 # isDaytime

  lbn r0 GasSensor Still RatioNitrogen Maximum
  lbn r1 GasSensor Still RatioPollutedWater Maximum 
  add r13 r0 r1
  snez r13 r13 # isDirty
 
  seqz r12 r14
  or r12 r12 r13 # isBoil

  lbn r0 GasSensor Still Temperature Average
  sgt r11 r0 TargetTemperature # isHot

  and r10 r14 r11
  sbn ActiveVent Still On r10 

  lbn r0 GasSensor Still RatioWater Maximum
  seqz r11 r0 # isEmpty

  or r10 r12 r11
  seqz r10 r10
  sbn LiquidDigitalValve Still On r10

  seqz r10 r12
  and r10 r10 r11
  sbn Stacker Still ClearMemory r10

  lbn r0 PipeAnalyzer RatioNitrogen Minimum
  blt r0 1 done
  beqz r14 done

  lbn r0 PipeAnalyzer Still TotalMoles Average
  lbn r1 PipeAnalyzer Still Temperature Average
  lbn r2 PipeAnalyzer Still Pressure Average
  lbn r3 GasTank Still TotalMoles Sum
  lbn r4 GasTank Still Temperature Average
  lbn r5 VolumePump Still Maximum Average
  lbn r6 VolumePump Still Maximum Sum

  # Compute the target volume of the pump
  sub r15 TargetTemperature r4
  mul r15 r15 r3
  mul r15 r15 r1
  mul r15 r15 IdealGasConstant
  sub r14 r1 TargetTemperature
  div r15 r15 r14
  div r15 r15 r2
  max r15 r15 0
  min r15 r15 r5
  sle r14 r1 TargetTemperature
  select r15 r14 r5 r15
  mul r15 r15 r5
  div r15 r15 r6
  snez r14 r15

  sbn VolumePump Still On r14
  sbn VolumePump Still Setting r15

done:
  yield
  j main
