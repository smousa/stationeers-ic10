# Configuration
define MaxPressure 51765.75 # kPa
define OutputTemperatureMin 277.2 # K
define Output2TemperatureMin 293.15 # K

# Properties
define PollutantRatio 1.698
define NitrousOxideRatio 0.558
define IdealGasConstant 8.314
define PollutantTemperature 425 # K
define PollutantPressure 6000 # kPa
define NitrousOxideTemperature 431 # K
define NitrousOxidePressure 2000 # kPa

# Structures
define PipeAnalyzer HASH("StructurePipeAnalysizer")
define DigitalValve HASH("StructureDigitalValve")
define VolumePump HASH("StructureVolumePump")
define PurgeValve HASH("StructurePurgeValve")

# Networks
define OutputPipe HASH("OutputPipeNetwork")
define Output2Pipe HASH("Output2PipeNetwork")
define OutputCoolingPipe HASH("OutputCoolingPipeNetwork")
define Output2CoolingPipe HASH("Output2CoolingPipeNetwork")

# Devices
alias Source db

main:
  move r15 0
  # Check filter life
  ls r0 Source 0 Quantity
  ls r1 Source 1 Quantity
  beqz r0 flushFiltration
  beqz r1 flushFiltration
  
  # Check pipe pressure/combustion
  l r0 Source PressureInput
  l r1 Source CombustionInput
  l r2 Source PressureOutput
  l r3 Source PressureOutput2
  beqz r0 flushFiltration
  bnez r1 flushFiltration
  bge r2 MaxPressure flushFiltration
  bge r3 MaxPressure flushFiltration
  
  # Check for pollutant condensation
  l r0 Source PressureOutput2
  l r1 Source RatioPollutantOutput2
  l r2 Source RatioLiquidPollutantOutput2
  select r14 r0 r1 1 # P > 0 && Pol == 0
  bgtz r2 flushFiltration
  beqz r14 flushFiltration
  
  # Compute mols needed to force condensation
  l r3 Source TotalMolesOutput2
  l r4 Source VolumeOutput2
  lbn r14 PipeAnalyzer Output2CoolingPipe TotalMoles Maximum
  add r3 r3 r14
  lbn r14 PipeAnalyzer Output2CoolingPipe Volume Maximum
  add r4 r4 r14
  mul r14 PollutantPressure r4
  div r14 r14 IdealGasConstant
  div r14 r14 PollutantTemperature
  mul r13 r3 r1
  slt r15 r13 r14
flushFiltration:
  s Source Mode r15

  # Output2 Digital Valve
  l Source r0 TemperatureOutput2
  sgt r15 r0 Output2TemperatureMin
  seqz r14 r15
  sbn DigitalValve Output2Pipe On r15
  sbn DigitalValve Output2CoolingPipe On r14

  # Compute Output Network Devices
  l r0 Source PressureOutput
  l r1 Source TemperatureOutput
  l r2 Source RatioVolatilesOutput
  l r3 Source RatioNitrousOxideOutput
  l r4 Source RatioLiquidNitrousOxideOutput
  # Output Digital Valve
  mul r15 r0 r3
  sgt r15 r15 NitrousOxidePressure
  select r15 r4 0 r15
  seqz r14 r15
  sbn DigitalValve OutputPipe On r15
  sbn DigitalValve OutputCoolingPipe On r14

  # Output Volume Pump
  add r15 r2 r3
  add r15 r15 r4
  seqz r15 r15
  select r15 r0 r15 0 
  sbn VolumePump OutputPipe On r15

  # Output Purge Valve
  sgtz r15 r2
  sbn PurgeValve OutputPipe On r15

yield
j main
