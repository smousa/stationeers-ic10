# Configuration
define MaxPressure 51765.75
define WasteTemperatureMin 291.5

# Properties
define PollutantTemperature 425 # K
define PollutantPressure 6000   # kPa

# Devices
alias Source db
alias VolumePump d0
alias PurgeValve d1

main:
 move r15 0
  # Check filter life
  ls r0 Source 0 Quantity
  ls r1 Source 1 Quantity
  beqz r0 flushFiltration
  beqz r1 flushFiltration
  
  # Check pipe pressure/combustion
  l r0 Source PressureInput
  l r1 Source CombustionInput
  l r2 Source PressureOutput
  l r3 Source PressureOutput2
  beqz r0 flushFiltration
  bnez r1 flushFiltration
  bge r2 MaxPressure flushFiltration
  bge r3 MaxPressure flushFiltration
  
  # Check for pollutant condensation
  l r4 Source RatioPollutantOutput2
  l r5 Source RatioLiquidPollutantOutput2
  bgt r5 r4 flushFiltration
  move r15 1
  beqz r3 flushFiltration

  # Compute mols needed to force condensation
  l r6 Source TotalMolesOutput2
  l r7 Source TemperatureOutput2
  mul r14 r6 r7
  div r14 r14 r3
  mul r14 r14 PollutantPressure
  div r14 r14 PollutantTemperature
  add r13 r4 r5
  mul r13 r13 r6
  slt r15 r13 r14
flushFiltration:
  s Source Mode r15

checkOutput:
  l r14 Source TemperatureOutput
  l r13 Source RatioVolatilesOutput
  l r12 Source RatioNitrousOxideOutput
  l r11 Source RatioLiquidNitrousOxideOutput
  move r15 0
  beqz r14 flushOutput
  bnez r13 flushOutput
  bnez r12 flushOutput
  bnez r11 flushOutput
  move r15 1

flushOutput:
  s VolumePump On r15
  snez r15 r13
  s PurgeValve On r15

yield
j main
